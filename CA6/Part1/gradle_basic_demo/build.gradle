plugins {
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'java'
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootJar {
    mainClass = 'payroll.PayrollApplication'
}

task cleanDeploy(type: Delete) {
    description = "Delete the dev deployment directory"
    delete "$buildDir/deployment/dev"
}

task copyArtifact(type: Copy) {
    description = "Copy the main application artifact (bootJar) to the deployment directory"
    dependsOn bootJar
    from { bootJar.archiveFile }
    into "$buildDir/deployment/dev"
}

task copyRuntimeDeps(type: Copy) {
    description = "Copy runtime JAR dependencies into deployment/lib"
    dependsOn classes
    from {
        configurations.runtimeClasspath.filter { it.name.endsWith('.jar') }
    }
    into "$buildDir/deployment/dev/lib"
}

task copyConfig(type: Copy) {
    description = "Copy properties files to deployment and replace tokens (version, buildTimestamp)"
    from('src/main/resources') {
        include '*.properties'
        filter(org.apache.tools.ant.filters.ReplaceTokens,
               tokens: [
                   version: (project.hasProperty('version') ? project.version : 'unspecified'),
                   buildTimestamp: new Date().format("yyyy-MM-dd'T'HH:mm:ss")
               ])
    }
    into "$buildDir/deployment/dev"
}

task deployToDev {
    description = "Prepare a dev deployment: clean -> artifact -> runtime libs -> configs"
    dependsOn cleanDeploy, copyArtifact, copyRuntimeDeps, copyConfig
    copyArtifact.mustRunAfter cleanDeploy
    copyRuntimeDeps.mustRunAfter copyArtifact
    copyConfig.mustRunAfter copyRuntimeDeps
    doLast {
        println "Deployment prepared at: ${buildDir}/deployment/dev"
    }
}

task runServer(type: JavaExec, dependsOn: classes) {
    group = "DevOps"
    description = "Launches a chat/server application"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'basic_demo.ChatServerApp'
    args '59001'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task backup(type: Copy) {
    group = "DevOps"
    description = "Backs up source files to the backup directory"
    from 'src'
    into 'backup/src'
    include '**/*.java'
}

tasks.register('zipBackup', Zip) {
    dependsOn tasks.named('backup')
    from 'backup/src'
    archiveFileName = "app-backup.zip"
    destinationDirectory = file("backup/zips")
}

task runInstalledApp {
    group = "DevOps"
    description = "Runs the app using the distribution script generated by installDist"
    dependsOn installDist
    doLast {
        def scriptName = "GradleProject_Transformation"
        def scriptExt = System.getProperty('os.name').toLowerCase().contains('win') ? '.bat' : ''
        def scriptPath = "$buildDir/install/${scriptName}/bin/${scriptName}${scriptExt}"
        println "Running installed app from: ${scriptPath}"
        if (System.getProperty('os.name').toLowerCase().contains('win')) {
            exec {
                commandLine 'cmd', '/c', scriptPath
            }
        } else {
            exec {
                commandLine 'bash', scriptPath
            }
        }
    }
}

tasks.register('javadocZip', Zip) {
    dependsOn tasks.named('javadoc')
    group = 'DevOps'
    description = 'Generate javadoc and zip the documentation'
    from tasks.named('javadoc').map { it.destinationDir }
    archiveFileName = 'javadoc.zip'
    destinationDirectory = file("$buildDir/docs")
}

sourceSets {
    integrationTest {
        java {
            srcDir 'src/integrationTest/java'
        }
        resources {
            srcDir 'src/integrationTest/resources'
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
}

check.dependsOn integrationTest

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
